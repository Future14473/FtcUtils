plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
    id "digital.wup.android-maven-publish" version "3.6.3"
}


repositories {
    //todo: not local
    mavenLocal()
    jcenter()
    maven {
        url 'https://dl.bintray.com/openftc/maven'
    }
}

dependencies {
    api "org.futurerobotics.jargon:core:0.1.0-SNAPSHOT"
    api "org.futurerobotics.jargon:blocks:0.1.0-SNAPSHOT"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3"
//    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.0'

    compileOnly "org.openftc:hardware-stock:5.3"
    compileOnly "org.openftc:robotcore-stock:5.3"
    api "org.openftc:rev-extensions-2:1.2"

    testImplementation "junit:junit:4.12"
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"


    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

//        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xuse-experimental=kotlin.Experimental"]
    }

}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}
dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/dokka"
}


task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat 'javadoc'
    outputDirectory "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier 'javadoc'
    from "$buildDir/javadoc"
}

publishing {
    publications {
        snapshot(MavenPublication) {
            group = 'org.futurerobotics.jargon'
            version = '0.1.0-SNAPSHOT'
            artifactId = "ftc-bridge"

            artifact sourcesJar
//            artifact javadocJar

            from components.android
        }
    }
}
